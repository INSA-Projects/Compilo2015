/*
 *     Projet Compilateur Yaka 2015
 *
 */


PARSER_BEGIN(Yaka)

public class Yaka {
  public static Declaration declaration;
  public static TabIdent tabIdent;
  public static Expression expression;
  public static YakaToAsm yvm;
  //public static final String ASMfilename = "org.asm";
  //public static final String YVMfilename = "code.jpeg";
  public static String YVMfilename;
 
  public static void main(String args[]) { 
    Yaka analyseur;

    yvm = new YakaToAsm();
    tabIdent = new TabIdent();
    expression = new Expression();
    declaration = new Declaration("");
    
    java.io.InputStream input;

    if (args.length==1) {
      System.out.print(args[args.length-1] + ": ");
      try {
        input = new java.io.FileInputStream(args[args.length-1]);
      } catch (java.io.FileNotFoundException e) {
        System.out.println("Fichier introuvable.");
        return;
      }
    } else if (args.length==0) {
      System.out.println("Lecture sur l'entree standard...");
      input = System.in;
    } else {
      System.out.println("Usage: java Yaka[fichier]");
      return;
    }
    try {
      analyseur = new Yaka(input);
      analyseur.prog();
      System.out.println("Analyse syntaxique réussie !");
    } catch (ParseException e) {
      String msg = e.getMessage();
      msg = msg.substring(0,msg.indexOf("\n"));
      System.out.println("Erreur de syntaxe : "+msg);
    }
  } 
}

PARSER_END(Yaka)

/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS :
{public static String identLu,chaineLue;
 public static int entierLu;}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);} 
}


/* Mots réservés*/

TOKEN :
{
  < BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME : "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">

}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ > 
	{ entierLu = Integer.parseInt(image.toString());
        }
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{identLu =image.toString();	
	}
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ chaineLue = image.toString();
	 }
}


/**************************************/
/********debut de la grammaire ********/
/**************************************/
void prog() : {}
{
   <PROGRAMME>  <ident> {yvm.setFileName(YakaTokenManager.identLu); yvm.entete(); }
   (declFonction())*
   <PRINCIPAL>{yvm.enteteMain();}
   bloc()
   <FPRINCIPAL>
   <FPROGRAMME> {yvm.queue();}
}

void bloc() : {}{
 (declConst())*
 (declVar())*
{ //ouvbloc : utilise le nombre de variables locales
	yvm.ouvreBlocFonction(tabIdent.tailleLocaux());}
   suiteInstr() 
}

void declConst() : {}
{
  <CONST>  defConst() 
  	( "," defConst() )* ";"
}

void defConst() : {}
{
	
  <ident> {declaration = new Declaration(YakaTokenManager.identLu);}	  	
   "=" valConst()
}

void valConst() : {}{
  <entier>  {declaration.setIdent( Type.ENTIER, YakaTokenManager.entierLu);}
 | <ident>  {declaration.setIdent(tabIdent.getType(YakaTokenManager.identLu), tabIdent.getValue(YakaTokenManager.identLu ));} 	
 | <VRAI>   {declaration.setIdent(Type.BOOLEEN, -1);}		
 | <FAUX>   {declaration.setIdent(Type.BOOLEEN, 0);}		
}


void declVar() : {}
{
  <VAR> type()	
  <ident> {declaration.setIdent(YakaTokenManager.identLu);yvm.alloc();}	
  ("," 
      <ident> {declaration.setIdent(YakaTokenManager.identLu);yvm.alloc();}
  )* ";"
	
}

void type() :{}
{
   <ENTIER> {declaration.setType(Type.ENTIER);}		
 | <BOOLEEN> {declaration.setType(Type.BOOLEEN);}	
}
/*
 * Syntaxe des instructions.
 */
void suiteInstr() : {}
{ 
	instruction() (";" (instruction())? )* 
}

void instruction() : {}
{
	affectation() | lecture() | ecriture() | iteration() | conditionnelle()| retourne()
}

void conditionnelle() : {} 
{
	<SI> expression() {expression.isBooleanExpression();}
	<ALORS> {yvm.iffauxCond();}
	suiteInstr() 
	{yvm.goTo(); yvm.sinon();}
	(
	<SINON> 
	suiteInstr()
	)?
	<FSI> {yvm.fsi();}
}
	

void iteration() : {}
{
	<TANTQUE> {yvm.tantQue();}
	expression()  {expression.isBooleanExpression(); } 
	<FAIRE> {yvm.faire();}
	suiteInstr() 
	<FAIT> {yvm.fait();}
}

void affectation() : {}
{
	<ident> {expression.setIdentAffected(YakaTokenManager.identLu);}
	"=" expression() {expression.controlTypeAffectation();}
}

void lecture() : {} 
{
	"LIRE("<ident>
	{yvm.lireEnt(tabIdent.findIdent(YakaTokenManager.identLu).getValue());}
	")"
}

void ecriture() : {}
{
	"ECRIRE("(expression() {yvm.ecrireEnt();}| <chaine> {yvm.ecrireChaine(YakaTokenManager.chaineLue);})")" 
	| "ALALIGNE" {yvm.aLaLigne();}
}
	

/*
 * Expression .
 */
void expression() : {}
{
  simpleExpr() 
  (opRel() 
  simpleExpr() {expression.controlType();}	
  )?
}



void simpleExpr() : {}
{
   terme() 
   (opAdd() 
   terme()	{expression.controlType();}	
   )*
} 
void terme() : {}
{
  facteur() 
  (opMul() 
  facteur()	{expression.controlType();}	
  )*
}
void facteur() : {}
{   primaire() 
  |  opNeg() primaire() {expression.controlType();}	
}

void primaire(): {}
{
   valeur() 
 | "(" expression() ")" 
 
}

void valeur() : {}
{ <entier> 	{expression.pushOperande(Type.ENTIER);
		 yvm.iconst(YakaTokenManager.entierLu);}
		 
 | <ident> 	{expression.pushOperande(tabIdent.getType(YakaTokenManager.identLu));
 		 expression.accedeIdent(YakaTokenManager.identLu); 
 		 // Cas où l'ident est un nom de fonction
 		 expression.nomFonctionAppelee = YakaTokenManager.identLu;
 		 }	
 		 
 		 
 		 
 (argumentsFonction() )? 

 | <VRAI> 	{expression.pushOperande(Type.BOOLEEN);
 		 yvm.iconst(-1);}	
 | <FAUX> 	{expression.pushOperande(Type.BOOLEEN);
 		 yvm.iconst(0);}	
 
}

void opRel() : {}
{
   "=" 		{expression.pushOperator(Operateur.EG);}	
 | "<>" 	{expression.pushOperator(Operateur.DIFF);}
 | "<" 		{expression.pushOperator(Operateur.INF);}
 | "<=" 	{expression.pushOperator(Operateur.INFEG);}
 | ">" 		{expression.pushOperator(Operateur.SUP);}
 | ">=" 	{expression.pushOperator(Operateur.SUPEG);}
} 

void opAdd() : {}
{
    "+"		{expression.pushOperator(Operateur.PLUS);}
  |  "-"  	{expression.pushOperator(Operateur.MOINS);}
  | <OU> 	{expression.pushOperator(Operateur.OU);}
}

void opMul() : {}
{
  "*"		{expression.pushOperator(Operateur.MULT);}
  | "/"		{expression.pushOperator(Operateur.DIV);}
  | <ET>	{expression.pushOperator(Operateur.ET);}
} 

void opNeg() : {}
{ "-" 		{expression.pushOperator(Operateur.NEG);}
 | <NON>	{expression.pushOperator(Operateur.NON);}
}

void declFonction() : {}
{
	type()
	<FONCTION>
	
	 { // creation de la fonction
	Declaration.fonctionDeclaration = new Function(declaration.getType());}
	
	<ident>
	
	{ // mémorisation du nom de la fonction dans la variable nomFonctionDeclaration
	String nomFonctionDeclaration = YakaTokenManager.identLu;}
	
	{// génération de l'entete de la fonction
	yvm.enteteFonction(nomFonctionDeclaration);}

	 
	paramForms()
	
	bloc()	
		
	
	
	<FFONCTION>
	
		{// ajout de la fonction dans tabIdent
	tabIdent.putFonction(nomFonctionDeclaration, Declaration.fonctionDeclaration);
	// Reset des locaux dans tabIdent
	tabIdent.clearLoco();}
	
	
	
	// fermeBloc : nombre de param * 2
	{yvm.fermeBloc(Declaration.fonctionDeclaration.tailleParams());}
	
}

void paramForms() : {}
{
	"(" (paramForm()  ("," paramForm())*)? ")" {Declaration.fonctionDeclaration.calculerOffsetsDesParametres();}
}

void paramForm() : {}
{
	type() {Type typeDuParametre = declaration.getType();}
	<ident> {	
				//	ajout du paramètre à la fonction en cours de déclaration : fonctionDeclaration (voir declFonction())		
				Declaration.fonctionDeclaration.addParametre(new Param(YakaTokenManager.identLu, typeDuParametre));
			}
}

void argumentsFonction() :{}
{
	"(" {expression.addFonctionAppelee((Function) tabIdent.findIdent(Expression.nomFonctionAppelee));}
	
	(expression() 
	
	// Ajout d'un type de paramètre à contrôler pour la fonction : le dernier type dans la pile opérandes ajouté par expression()
	{expression.getFonctionAppelee().addParamToControl(expression.popTypeOperande());}
	
	// Idem si d'autre paramètres : ajout du type dans paramètres à contrôler pour la fonction appelée
	
	("," expression() 
	
	{expression.getFonctionAppelee().addParamToControl(expression.popTypeOperande());} )*)? ")"
	
	{expression.getFonctionAppelee().controlTypeParam(); 
		// Depiler la pile de fonctions
		expression.removeFonctionAppelee();
	}
}

void retourne() : {}
{
	<RETOURNE>

	expression()
	
			{// retourne : utilise tailleParam+2
	yvm.retourne(Declaration.fonctionDeclaration.tailleParams()+4);}
}


	
	
